plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'cn.gekal'
version '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': project.group,
                'Automatic-Module-Name': 'cn.gekal.jasperreports.japanese.fonts'
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'JasperReports Japanese Fonts'
                description = 'Japanese fonts extension for JasperReports.'
                url = 'https://github.com/gekal/jasperreports-japanese-fonts'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'gekal'
                        name = 'Gekal'
                        email = 'gekal@gekal.cn'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/gekal/jasperreports-japanese-fonts.git'
                    developerConnection = 'scm:git:ssh://github.com/gekal/jasperreports-japanese-fonts.git'
                    url = 'https://github.com/gekal/jasperreports-japanese-fonts'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.findProperty("ossrhUsername")
                password = project.findProperty("ossrhPassword")
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
