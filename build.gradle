plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.22.0'
}

group = 'cn.gekal'
version = project.findProperty('version') ?: '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': project.group,
                'Automatic-Module-Name': 'cn.gekal.jasperreports.japanese.fonts'
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            group = 'cn.gekal'
            version = project.version

            from components.java

            pom {
                name = 'JasperReports Japanese Fonts'
                description = 'Japanese fonts extension for JasperReports.'
                url = 'https://github.com/gekal-study-jasperreports/jasperreports-japanese-fonts'
                inceptionYear = '2026'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                    license {
                        name = 'IPA Font License Agreement v1.0'
                        url = 'https://fonts.jp/ipaex_licenses/'
                    }
                }
                developers {
                    developer {
                        name = 'Gekal'
                        email = 'liu.hongying@gekal.cn'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/gekal-study-jasperreports/jasperreports-japanese-fonts.git'
                    developerConnection = 'scm:git:ssh://github.com/gekal-study-jasperreports/jasperreports-japanese-fonts.git'
                    url = 'https://github.com/gekal-study-jasperreports/jasperreports-japanese-fonts'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    project {
        name = 'jasperreports-japanese-fonts'
        version = project.version
        description = 'Japanese fonts extension for JasperReports.'
        longDescription = 'Japanese fonts extension for JasperReports.'
        links {
            homepage = 'https://github.com/gekal-study-jasperreports/jasperreports-japanese-fonts'
        }
        authors = ['Gekal']
        license = 'Apache-2.0'
        inceptionYear = '2026'
    }
    release {
        github {
            overwrite = true
            changelog {
                formatted = 'ALWAYS'
                preset = 'conventional-commits'
            }
        }
    }
    signing {
        pgp {
            active = 'ALWAYS'
            armored = true
        }
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}
